#region Prolog

#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################


cControlPrefix = '}';
#*** Log File Name
cConfigDim = cControlPrefix | 'tp_config';
If (DimensionExists(cConfigDim) = 1);
	cGenerateLog = ATTRS(cConfigDim, 'Generate TI Log', 'String Value');
Else;
	cGenerateLog = 'N';
EndIf;

cTM1Process = GetProcessName();
StringGlobalVariable('gPrologLog');
StringGlobalVariable('gEpilogLog');
StringGlobalVariable('gDataLog');

IF (cGenerateLog @= 'Y' % cGenerateLog @= 'T');
vReturnValue = ExecuteProcess(cControlPrefix | 'tp_get_log_file_names', 'pExecutionId', pExecutionId,
'pProcess', cTM1Process, 'pControl', 'Y');
If (vReturnValue <> ProcessExitNormal());
	ProcessError;
EndIf;
Endif;

cPrologLog = gPrologLog;
cEpilogLog = gEpilogLog;
cDataLog = gDataLog;
cTM1Log = cPrologLog;

#***
cApprovalDim =  ATTRS('}tp_applications', pAppId,  'ApprovalDimension');
cApprovalSubset =ATTRS('}tp_applications', pAppId,  'ApprovalSubset');
cShadowApprovalDim ='}tp_tasks}' | pAppId;

#***

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, 'get application attributes', cApprovalDim, cApprovalSubset, cShadowApprovalDim );
EndIf;

#***
cApprovalDimToCompare  =  ATTRS('}tp_applications', pAppIdToCompare,  'ApprovalDimension');
cApprovalSubsetToCompare =ATTRS('}tp_applications', pAppIdToCompare,  'ApprovalSubset');
cShadowApprovalDimToCompare  ='}tp_tasks}' | pAppIdToCompare;

#***
If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, 'get application_to_compare attributes', cApprovalDimToCompare, 
		cApprovalSubsetToCompare, cShadowApprovalDimToCompare );
EndIf;

#***
isApprovalHierarchyOverlap = 'N';

IF (cApprovalDim @= cApprovalDimToCompare & cApprovalSubset @=cApprovalSubsetToCompare);
	isApprovalHierarchyOverlap = 'Y';
Elseif (cApprovalDim @= cApprovalDimToCompare & cApprovalSubset @<>cApprovalSubsetToCompare);
	vSubset = 'approval_overlap' | pExecutionId;
	IF (SubsetExists(cApprovalDim, vSubset) =1);
		SubsetDestroy(cApprovalDim, vSubset);
	Endif;
	
	vMdx = '{INTERSECT(Tm1SubsetToSet( [' | cApprovalDim | '], "' | cApprovalSubset | '" ) , Tm1SubsetToSet( [' | cApprovalDimToCompare | '], "' | cApprovalSubsetToCompare | '" ) )}';
	SubsetCreateByMdx(vSubset, vMdx, cApprovalDim);
	vTotalOverlap = SubsetGetSize(cApprovalDim, vSubset);
	IF (vTotalOverlap >0);
		isApprovalHierarchyOverlap = 'Y';
	Endif;
	If (cGenerateLog @= 'Y');
		TextOutput(cTM1Log, vMdx, NumberToString(vTotalOverlap) );
	EndIf;
Endif;

If (isApprovalHierarchyOverlap @= 'Y');
	vReturnValue = ExecuteProcess(cControlPrefix | 'tp_error_update_error_cube', 
		'pGuid', pExecutionId, 
		'pProcess', cTM1Process, 
		'pErrorCode', 'TI_APPROVAL_OVERLAPPED',
		'pErrorDetails', pAppId,
		'pControl', 'Y');
	
EndIf;
#endregion
#region Epilog
#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################




#*** Log File Name
cTM1Log = cEpilogLog;


IF (SubsetExists(cApprovalDim, vSubset) =1);
	SubsetDestroy(cApprovalDim, vSubset);
Endif;

#*** No error
If (cGenerateLog @= 'Y');
TextOutput(cTM1Log, 'The end has been reached.');
EndIf;
#endregion