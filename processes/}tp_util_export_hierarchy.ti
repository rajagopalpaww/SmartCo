#region Prolog


#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################

DatasourceASCIIDelimiter = char(9);
DatasourceASCIIQuoteCharacter='';

cControlPrefix = '';
If (pControl @= 'Y');
	cControlPrefix = '}';
EndIf;


#*** Log File Name
cConfigDim = cControlPrefix | 'tp_config';
If (DimensionExists(cConfigDim) = 1);
	cGenerateLog = ATTRS(cConfigDim, 'Generate TI Log', 'String Value');
Else;
	cGenerateLog = 'N';
EndIf;

cTM1Process = GetProcessName();
StringGlobalVariable('gPrologLog');
StringGlobalVariable('gEpilogLog');
StringGlobalVariable('gDataLog');

If (cGenerateLog @= 'Y' % cGenerateLog @= 'T');
	vReturnValue = ExecuteProcess(cControlPrefix | 'tp_get_log_file_names', 'pExecutionId', pExecutionId,
	'pProcess', cTM1Process, 'pControl', pControl);
	
	If (vReturnValue <> ProcessExitNormal());
		processError;
	EndIf;
EndIf;

cPrologLog = gPrologLog;
cEpilogLog = gEpilogLog;
cDataLog = gDataLog;
cTM1Log = cPrologLog;

#***

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 'Parameters: ', 
	pDim, pSubset, pBlob, pControl);
EndIf;

If (DimensionExists(pDim) = 0);
	ProcessError;
EndIf;

If (SubsetExists(pDim, pSubset) = 0);
	ProcessError;
EndIf;

#***
StringGlobalVariable('gTopNode');
NumericGlobalVariable('gTopLevel');

vReturnValue = ExecuteProcess(cControlPrefix | 'tp_get_top_node',
'pExecutionId', pExecutionId, 'pDim', pDim, 'pSubset', pSubset);
If (vReturnValue <> ProcessExitNormal());
	ProcessError;
EndIf;

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 
	'tp_get_top_node', gTopNode, NumberToString(gTopLevel));
EndIf;

StringGlobalVariable('gIsInSubset');

cSubsetSize = SubsetGetSize(pDim, pSubset);
cTopPNode = DimensionElementPrincipalName(pDim, gTopNode);

vIndex = 1;
While (vIndex <= cSubsetSize);
	vNode = SubsetGetElementName(pDim, pSubset, vIndex);
	vPNode = DimensionElementPrincipalName(pDim, vNode);
	
	vType = DTYPE(pDim, vPNode);
	
	vRealParent = '';
	If (vPNode @<> cTopPNode);		
		vParentCount = ELPARN(pDim, vPNode);
		
		If (vParentCount = 1);
			vRealParent = ELPAR(pDim, vPNode, 1);
		Else;
			vGap = 0;
			vParentIndex = 1;
			While (vParentIndex <= vParentCount);
				vParent = ELPAR(pDim, vPNode, vParentIndex);
							
				vParentIndexI = 0;
				vIndexI = 1;
				While (vIndexI <= cSubsetSize);
					vNodeI = SubsetGetElementName(pDim, pSubset, vIndexI);
					vPNodeI = DimensionElementPrincipalName(pDim, vNodeI);
					If (vParent @= vPNodeI);
						vParentIndexI = vIndexI;
						vIndexI = cSubsetSize;
					EndIf;
								
					vIndexI = vIndexI + 1;
				End;
		
				If (vParentIndexI > 0 & vIndex > vParentIndexI);
					If (vGap = 0 % vGap > vIndex - vParentIndexI);
						vRealParent = vParent;
						vGap = vIndex - vParentIndexI;
					EndIf;
				EndIf;
			
				vParentIndex = vParentIndex + 1;
			End;
		
			If (vRealParent @= '');
				ProcessError;
			EndIf;
		EndIf;
	EndIf;

    If (pIncludeWeight @= 'Y');
    	If (vRealParent @= '');
    		vWeight = 0.0;
    	Else;
			vWeight = ELWEIGHT(pDim, vRealParent, vPNode);
		EndIf;
	EndIf;
	
	SetOutputCharacterSet(pBlob | '.blb', 'TM1CS_UTF8');
	If (pIncludeWeight @= 'Y');
		TextOutput(pBlob | '.blb', vRealParent, vPNode, vType, NumberToString(vWeight));
	Else;
		TextOutput(pBlob | '.blb', vRealParent, vPNode, vType); 
	EndIf;

	vIndex = vIndex + 1;
End;

#*** No error

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 'The end has been reached.');
EndIf;


#endregion