#region Prolog


#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################

#*** Log File Name
cConfigDim = '}tp_config';
If (DimensionExists(cConfigDim) = 1);
	cGenerateLog = ATTRS(cConfigDim, 'Generate TI Log', 'String Value');
Else;
	cGenerateLog = 'N';
EndIf;

cTM1Process = GetProcessName();
StringGlobalVariable('gPrologLog');
StringGlobalVariable('gEpilogLog');
StringGlobalVariable('gDataLog');

IF (cGenerateLog @= 'Y' % cGenerateLog @= 'T');
	vReturnValue = ExecuteProcess('}tp_get_log_file_names', 'pExecutionId', pExecutionId,
	'pProcess', cTM1Process, 'pControl', 'Y');
	If (vReturnValue <> ProcessExitNormal());
		ProcessError;
	EndIf;
EndIf;

cPrologLog = gPrologLog;
cEpilogLog = gEpilogLog;
cDataLog = gDataLog;
cTM1Log = cPrologLog;
#***

cApprovalDim =  ATTRS('}tp_applications', pAppId,  'ApprovalDimension');
cShadowApprovalDim = ATTRS('}tp_applications', pAppId, 'ApprovalShadowDimension');
cControlDim = ATTRS('}tp_applications', pAppId,  'VersionDimension');
cWritableSlicesOld = ATTRS('}tp_applications', pAppId,  'VersionSlicesWriteOld');
cWritableSlicesNew = ATTRS('}tp_applications', pAppId,  'VersionSlicesWrite');
cStateCube = '}tp_application_state}' | pAppId;
cNodeInfoDim = '}tp_node_info';
cStateField = 'State';
cOwnerIdField = 'CurrentOwnerId';
cOwnershipNodeField = 'TakeOwnershipNode';
vSingleQ = '''';

IF (cWritableSlicesOld @<> cWritableSlicesNew);
	vMdx = '{FILTER([' | cShadowApprovalDim | '].MEMBERS, [' | cStateCube | '].[' | cNodeInfoDim | '].[' | cStateField | '] =' | vSingleQ | '2' | vSingleQ | ')}';
	vSubsetOwnedNodes = 'tp_temp_owned_nodes';
	IF (SubsetExists(cShadowApprovalDim, vSubsetOwnedNodes) =1);
		SubsetDestroy(cShadowApprovalDim,vSubsetOwnedNodes);
	Endif;

	SubsetCreateByMdx(vSubsetOwnedNodes, vMdx, cShadowApprovalDim);

	vTotalOwnedNodes = SubsetGetSize(cShadowApprovalDim,vSubsetOwnedNodes );

	vLooper =1 ;

	While (vLooper <= vTotalOwnedNodes);
		vOwnedNode = SubsetGetElementName(cShadowApprovalDim, vSubsetOwnedNodes, vLooper);
		IF (DTYPE(cShadowApprovalDim, vOwnedNode) @<> 'C');

			vOwner = CellGetS(cStateCube, vOwnedNode, cOwnerIdField);
			vOwnershipNode = CellGetS(cStateCube, vOwnedNode, cOwnershipNodeField);
			#First release all old DRs on old control slices
			vReturnValue = ExecuteProcess('}tp_workflow_util_reserve_cube_slices', 'pExecutionId', pExecutionId, 
				'pAppId', pAppId, 'pCube', pCube, 'pApprovalDim', cApprovalDim, 'pNode', vOwnershipNode,  'pReserve', 'N', 
				'pUser', vOwner, 'pControlDim', cControlDim, 'pControlWritableSlices', cWritableSlicesOld );

			If (vReturnValue <> ProcessExitNormal());
				ProcessError;
			EndIf;

			If (cGenerateLog @= 'Y');
				TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 'DR released on node: ' | vOwnedNode | ', user: ' | vOwner | ', cube: ' | pCube);
			EndIf;

			#Next reacquire new DRs on new control slices
			vReturnValue = ExecuteProcess('}tp_workflow_util_reserve_cube_slices', 'pExecutionId', pExecutionId, 
				'pAppId', pAppId, 'pCube', pCube, 'pApprovalDim', cApprovalDim, 'pNode', vOwnershipNode,  'pReserve', 'Y', 
				'pUser', vOwner, 'pControlDim', cControlDim, 'pControlWritableSlices', cWritableSlicesNew );

			If (vReturnValue <> ProcessExitNormal());
				ProcessError;
			EndIf;

			If (cGenerateLog @= 'Y');
				TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 'DR reacquired on node: ' | vOwnedNode | ', user: ' | vOwner | ', cube:' | pCube);
			EndIf;

		Endif;
		vLooper = vLooper +1;
	End;

Endif;

#*** No error

If (cGenerateLog @= 'Y');
TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 'The end has been reached.');
EndIf;
#endregion
#region Epilog


#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################

#*** Log File Name
cTM1Log = cEpilogLog;

#***
IF (SubsetExists(cShadowApprovalDim, vSubsetOwnedNodes) =1);
SubsetDestroy(cShadowApprovalDim,vSubsetOwnedNodes);
Endif;

#*** No error

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 'The end has been reached.');
EndIf;
#endregion