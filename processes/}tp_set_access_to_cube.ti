#region Prolog


#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################


cControlPrefix = '';
If (pControl @= 'Y');
cControlPrefix = '}';
EndIf;

#*** Log File Name
cTM1Process = cControlPrefix | 'tp_set_access_to_cube';
StringGlobalVariable('gPrologLog');
StringGlobalVariable('gEpilogLog');
StringGlobalVariable('gDataLog');
vReturnValue = ExecuteProcess(cControlPrefix | 'tp_get_log_file_names',
'pExecutionId', pExecutionId, 'pProcess', cTM1Process, 'pControl', pControl);
If (vReturnValue <> ProcessExitNormal());
ProcessError;
EndIf;
cPrologLog = gPrologLog;
cEpilogLog = gEpilogLog;
cDataLog = gDataLog;
cTM1Log = cPrologLog;

cConfigDim = cControlPrefix | 'tp_config';
If (DimensionExists(cConfigDim) = 1);
cGenerateLog = ATTRS(cControlPrefix | 'tp_config', 'Generate TI Log', 'String Value');
Else;
cGenerateLog = 'N';
EndIf;

#*** Log Parameters

If (cGenerateLog @= 'Y');
TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 'Parameters:',
pExecutionId, pSourceFile, pControl);
EndIf;

#*** Set local variables
DataSourceType = 'CHARACTERDELIMITED';
DatasourceASCIIDelimiter = CHAR(9);
DatasourceASCIIHeaderRecords = 0;
DatasourceNameForServer = pSourceFile;

If (cGenerateLog @= 'Y');
TextOutput(cTM1Log, 'DataSourceType: ' | DataSourceType);
TextOutput(cTM1Log, 'DatasourceASCIIDelimiter: ' | DatasourceASCIIDelimiter);
TextOutput(cTM1Log, 'DatasourceASCIIHeaderRecords: ' | NumberToString(DatasourceASCIIHeaderRecords));
TextOutput(cTM1Log, 'DatasourceNameForServer: ' | DatasourceNameForServer);
EndIf;

#*** Set input file encoding as UTF-8

SetInputCharacterSet('TM1CS_UTF8');

#*** Check TM1 security right

cAdmin = 'ADMIN';
cLock = 'LOCK';
cReserve = 'RESERVE';
cWrite = 'WRITE';
cRead = 'READ';
cNone = 'NONE';

vRight = '';
If (pRight @= cAdmin);
vRight = cAdmin;
ElseIf (pRight @= cLock);
vRight = cLock;
ElseIf (pRight @= cReserve);
vRight = cReserve;
ElseIf (pRight @= cWrite);
vRight = cWrite;
ElseIf (pRight @= cRead);
vRight = cRead;
ElseIf (pRight @= cNone);
vRight = cNone;
Else;

vReturnValue = ExecuteProcess(cControlPrefix | 'tp_error_update_error_cube',
'pGuid', pExecutionId,
'pProcess', cTM1Process,
'pErrorCode', 'TI_WRONG_PERMISSION',
'pErrorDetails', pRight | ', ' | pCube,
'pControl', pControl);

ProcessError;
EndIf;

#*** No error

If (cGenerateLog @= 'Y');
TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 'The end has been reached.');
EndIf;

#endregion
#region Data


#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################

cCubeSecurityCube = '}CubeSecurity';
cDimensionSecurityCube = '}DimensionSecurity';
cElementAttributesPrefix = '}ElementAttributes_';

CellPutS(vRight, cCubeSecurityCube, pCube, varGroup);

vIndex = 1;
While (vIndex <> 0);
vDim = TABDIM(pCube, vIndex);

If (vDim @<> '' & vRight @<> cNone);

CellPutS(cRead, cDimensionSecurityCube, vDim, varGroup);
vElementAttributes = cElementAttributesPrefix | vDim;
If (DimensionExists(vElementAttributes) <> 0);
CellPutS(cRead, cDimensionSecurityCube, cElementAttributesPrefix | vDim, varGroup);
EndIf;
If (CubeExists(vElementAttributes) <> 0);
CellPutS(cRead, cCubeSecurityCube, cElementAttributesPrefix | vDim, varGroup);
EndIf;

EndIf;

If (vDim @<> '');
vIndex = vIndex + 1;
Else;
vIndex = 0;
EndIf;
End;


#endregion