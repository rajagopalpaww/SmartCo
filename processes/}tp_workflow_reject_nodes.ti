#region Prolog


#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################

cControlPrefix = '';
If (pControl @= 'Y');
cControlPrefix = '}';
EndIf;

#*** Log File Name
cConfigDim = cControlPrefix | 'tp_config';
If (DimensionExists(cConfigDim) = 1);
	cGenerateLog = ATTRS(cConfigDim, 'Generate TI Log', 'String Value');
Else;
	cGenerateLog = 'N';
EndIf;

cTM1Process = GetProcessName();
StringGlobalVariable('gPrologLog');
StringGlobalVariable('gEpilogLog');
StringGlobalVariable('gDataLog');

IF (cGenerateLog @= 'Y' % cGenerateLog @= 'T');
vReturnValue = ExecuteProcess(cControlPrefix | 'tp_get_log_file_names', 'pExecutionId', pExecutionId,
'pProcess', cTM1Process, 'pControl', pControl);
If (vReturnValue <> ProcessExitNormal());
	ProcessError;
EndIf;
Endif;

cPrologLog = gPrologLog;
cEpilogLog = gEpilogLog;
cDataLog = gDataLog;
cTM1Log = cPrologLog;

#***
cApprovalDim =  ATTRS('}tp_applications', pAppId,  'ApprovalDimension');
cApprovalSubset =ATTRS('}tp_applications', pAppId,  'ApprovalSubset');
cIsActive =ATTRS('}tp_applications', pAppId, 'IsActive');
cShadowApprovalDim =ATTRS('}tp_applications', pAppId, 'ApprovalShadowDimension');

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, 'get application attributes', cApprovalDim, cApprovalSubset, cIsActive,cShadowApprovalDim );
EndIf;


If (cIsActive @<> 'Y');
	vReturnValue = ExecuteProcess(cControlPrefix | 'tp_error_update_error_cube', 
		'pGuid', pExecutionId, 
		'pProcess', cTM1Process, 
		'pErrorCode', 'TI_APP_NOT_ACTIVE',
		'pErrorDetails', pAppId,
		'pControl', pControl);
	
	ProcessError;
EndIf;


#*** Log Parameters

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 'Parameters:', pExecutionId, pTime, pAppId, pNode, pControl);
EndIf;

#*** Check state cube

cStateCube = cControlPrefix | 'tp_application_state}' | pAppId;

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, 'Check state cube: ' | cStateCube);
EndIf;

If(CubeExists(cStateCube) = 0);
	vReturnValue = ExecuteProcess(cControlPrefix | 'tp_error_update_error_cube', 
		'pGuid', pExecutionId, 
		'pProcess', cTM1Process, 
		'pErrorCode', 'TI_CUBE_NOT_EXIST',
		'pErrorDetails', cStateCube,
		'pControl', pControl);

	ProcessError;
EndIf;

#***

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, 'Check node: ' | pNode);
EndIf;

If (DIMIX(cShadowApprovalDim, pNode) = 0);
ProcessError;
EndIf;

#*** State constants

cNotStarted = '0';
cIncomplete = '1';
cWorkInProgress = '2';
cReady = '3';
cLocked = '4';

#*** Change node state
StringGlobalVariable('gParentInSubset');
cState= 'State';

#***
#First step: loop all parent nodes and put locked ones into a temp subset 
##the user must have Reject permission on each locked parent node
#Second Step:  unlock any locked parents in temp subset,
#Start with the top most parent lower subset index and walk down the tree
#*** Define global variables
cNodeEsc = '';
If (SCAN(',', pNode) = 0);
	cNodeEsc = pNode;
Else;
	cNodeLength = LONG(pNode);
	looper = 1;
	While (looper <= cNodeLength);
		cIdChar = SUBST(pNode, looper, 1);
		If (cIdChar @= ',');
			cNodeEsc = cNodeEsc | '_';
		Else;
			cNodeEsc = cNodeEsc | cIdChar;
		EndIf;
		
		looper = looper + 1;
	End;
EndIf;

#create a temp subset
vTempSubset = 'tp_temp_parents_to_reject_' | cNodeEsc | '_' | pTime;
IF (SubsetExists(cApprovalDim, vTempSubset) >1);
	SubsetDestroy(cApprovalDim, vTempSubset);
Endif;
SubsetCreate(cApprovalDim, vTempSubset);
SubsetElementInsert(cApprovalDim, vTempSubset, pNode, 1);

vReturnValue = ExecuteProcess(cControlPrefix | 'tp_get_parent_in_subset', 'pExecutionId', pExecutionId,
	'pDim', cShadowApprovalDim, 'pSubset', cApprovalSubset, 'pNode', pNode);
If (vReturnValue <> ProcessExitNormal());
	ProcessError;
EndIf;

vNodeParent = gParentInSubset;
While (vNodeParent @<> '');
	vNodeParentState = CellGetS(cStateCube, vNodeParent, cState);

	#*** Check user  privilege
	If (vNodeParentState @= cLocked);
		StringGlobalVariable('gReject');
		vReturnValue = ExecuteProcess(cControlPrefix | 'tp_get_user_permissions', 
			'pGuid', pExecutionId, 'pApplication', pAppId, 'pNode',  vNodeParent, 'pUser', TM1User, 'pControl', pControl);
		If (vReturnValue <> ProcessExitNormal());
			ProcessError;
		EndIf;

		If (gReject @= 'F');
			IF (SubsetExists(cApprovalDim, vTempSubset)>0);
				SubsetDestroy(cApprovalDim, vTempSubset);	
			Endif;

			vReturnValue = ExecuteProcess(cControlPrefix | 'tp_error_update_error_cube', 
				'pGuid', pExecutionId, 
				'pProcess', cTM1Process, 
				'pErrorCode', 'TI_NO_PERMISSION_DETAILS',
				'pErrorDetails', 'REJECT' | ', ' |  vNodeParent,
				'pControl', pControl);

			ProcessError;

		EndIf;

		#Insert this parent node into a temp subset in approval dimension
		#Always insert into the first position
		SubsetElementInsert(cApprovalDim, vTempSubset, vNodeParent, 1);
	Endif;

	vReturnValue = ExecuteProcess(cControlPrefix | 'tp_get_parent_in_subset', 'pExecutionId', pExecutionId,
		'pDim', cShadowApprovalDim, 'pSubset', cApprovalSubset, 'pNode',  vNodeParent);
	If (vReturnValue <> ProcessExitNormal());
		ProcessError;
	EndIf;
	vNodeParent = gParentInSubset;
End;






#endregion
#region Epilog


#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################

#***
#Now loop through the temp subset and reject node from top down
totalRejectNodes = SubsetGetSize(cApprovalDim, vTempSubset);
looper = 1;
While (looper <= totalRejectNodes);
	vNodeToReject = SubsetGetElementName(cApprovalDim, vTempSubset, looper);
	vReturnValue = ExecuteProcess(cControlPrefix | 'tp_workflow_reject_node', 
		'pExecutionId', pExecutionId, 'pTime', pTime, 'pAppId', pAppId, 'pNode',  vNodeToReject,  'pControl', pControl);
	If (vReturnValue <> ProcessExitNormal());
		ProcessError;
	EndIf;

	looper = looper +1;
End;

IF (SubsetExists(cApprovalDim, vTempSubset)>0);
	SubsetDestroy(cApprovalDim, vTempSubset);	
Endif;


#endregion