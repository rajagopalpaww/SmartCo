#region Prolog
#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################

cControlPrefix = '}';

#*** Log File Name
cConfigDim = cControlPrefix | 'tp_config';
If (DimensionExists(cConfigDim) = 1);
	cGenerateLog = ATTRS(cConfigDim, 'Generate TI Log', 'String Value');
Else;
	cGenerateLog = 'N';
EndIf;

cTM1Process = GetProcessName();
StringGlobalVariable('gPrologLog');
StringGlobalVariable('gEpilogLog');
StringGlobalVariable('gDataLog');

IF (cGenerateLog @= 'Y' % cGenerateLog @= 'T');
	vReturnValue = ExecuteProcess(cControlPrefix | 'tp_get_log_file_names', 'pExecutionId', pExecutionId,
	'pProcess', cTM1Process, 'pControl', 'Y');
	If (vReturnValue <> ProcessExitNormal());
		ProcessError;
	EndIf;
Endif;

cPrologLog = gPrologLog;
cEpilogLog = gEpilogLog;
cDataLog = gDataLog;
cTM1Log = cPrologLog;

#***
cTaskNavDimensions = '}tp_task_navigation_dims}' | pAppId;
cTaskNavigationCube = '}tp_task_navigations}' | pAppId;

#***
IF (pControlDim @='');
cVersionDim = ATTRS('}tp_applications', pAppId, 'VersionDimension');
Else;
cVersionDim = pControlDim;
Endif;

IF (pControlWritableSlices @='');
cVersionSlicesWrite =ATTRS('}tp_applications', pAppId, 'VersionSlicesWrite');
Else;
cVersionSlicesWrite = pControlWritableSlices;
Endif;

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, 'get application attributes', cVersionDim,cVersionSlicesWrite );
EndIf;

#*** Log Parameters

If (cGenerateLog @= 'Y');
TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), 'Parameters:', pExecutionId,  pAppId, pCube, pApprovalDim, pNode, pLock);
EndIf;


#***Lock slices

cOverlayDim = '}SecurityOverlay';		
versionSeparater = '|';
vPosVersion = 0;
vStringToScan = cVersionSlicesWrite;
IF (cVersionSlicesWrite @<> '');
	vPosVersion = SCAN(versionSeparater, vStringToScan);
Else;
	vPosVersion = 1;
Endif;

While (vPosVersion >0);
	vVersionSlice  = SUBST(vStringToScan, 1, vPosVersion-1);
	vStringToScan = SUBST(vStringToScan, vPosVersion +1, LONG(vStringToScan)-vPosVersion);
	vPosVersion = SCAN(versionSeparater, vStringToScan);
	cCubeAddress = '';
	addConcatSymbol = 0;
	cOverlayCube = '}SecurityOverlayGlobal_' | pCube;

	dimensionIndex = 1;
	While (dimensionIndex > 0 ); 
		cCubeDimensionName = TABDIM(cOverlayCube, dimensionIndex);
		If (cCubeDimensionName @= '');
			dimensionIndex = -1;
		Else;
			If (addConcatSymbol > 0);
				IF (cCubeDimensionName @<> cOverlayDim);
					cCubeAddress = cCubeAddress | '|';
				Endif;

			Else;
				addConcatSymbol = 1;
			EndIf; 

			IF (cCubeDimensionName @=cVersionDim & vVersionSlice @<>'');
				IF (DIMIX(cVersionDim, vVersionSlice)=0);
					vReturnValue = ExecuteProcess('}tp_error_update_error_cube',
						'pGuid', pExecutionId,
						'pProcess', cTM1Process,
						'pErrorCode', 'TI_WRITABLE_SLICE_NOT_EXISTS',
						'pErrorDetails', cVersionDim  | '.' | vVersionSlice | ', ' |  pAppId,
						'pControl', 'Y');
	
					ProcessError;
				Endif;
				cCubeAddress = cCubeAddress | vVersionSlice; 
			Endif;

			vTotalNavDimensions = DIMSIZ(cTaskNavDimensions);
			vNavLooper = 1;
			While (vNavLooper <= vTotalNavDimensions);
				vNavDimension = DIMNM(cTaskNavDimensions, vNavLooper);
				IF (cCubeDimensionName @= vNavDimension);
					vNavElem = CellGetS(cTaskNavigationCube, pNode, vNavDimension, 'NavigationElement');
					IF (vNavElem @<>'');
						cCubeAddress = cCubeAddress | vNavElem;
					Endif;
				Endif;

				vNavLooper = vNavLooper +1;
			End;

			#IF (cCubeDimensionName @= pApprovalDim & pNode @<> '');
			#	cCubeAddress = cCubeAddress | pNode;
			#Endif;
		EndIf;
			
		If (cGenerateLog @= 'Y');
			cLogCubeText = 'Lock address = ' | cCubeAddress;
			TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), cLogCubeText);
		EndIf;

		dimensionIndex = dimensionIndex + 1;
	End;

	IF (CubeExists(cOverlayCube)>0);
		IF (pLock @= 'Y');
			SecurityOverlayGlobalLockNode(1,pCube,cCubeAddress);
		Else;
			SecurityOverlayGlobalLockNode(0,pCube,cCubeAddress);
		Endif;
			
	Endif;

	If (cGenerateLog @= 'Y');
		cLogCubeText = 'lock cube: ' | pCube | ' on address: ' | cCubeAddress;
		TextOutput(cTM1Log, TIMST(NOW, '\Y-\m-\d \h:\i:\s'), cLogCubeText);
	EndIf;
end;

#*** No error

If (cGenerateLog @= 'Y');
TextOutput(cTM1Log, 'The end has been reached.');
EndIf;
#endregion