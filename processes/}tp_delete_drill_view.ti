#region Prolog
#################################################################
## IBM Confidential
##
## OCO Source Materials
##
## BI and PM: pmpsvc
##
## (C) Copyright IBM Corp. 2008, 2009, 2010
##
## The source code for this program is not published or otherwise
## divested of its trade secrets, irrespective of what has been
## deposited with the U.S. Copyright Office.
#################################################################


cControlPrefix = '';
If (pControl @= 'Y');
	cControlPrefix = '}';
EndIf;


#*** Log File Name
cConfigDim = cControlPrefix | 'tp_config';
If (DimensionExists(cConfigDim) = 1);
	cGenerateLog = ATTRS(cConfigDim, 'Generate TI Log', 'String Value');
Else;
	cGenerateLog = 'N';
EndIf;

cTM1Process = GetProcessName();
StringGlobalVariable('gPrologLog');
StringGlobalVariable('gEpilogLog');
StringGlobalVariable('gDataLog');

IF (cGenerateLog @= 'Y' % cGenerateLog @= 'T');
	vReturnValue = ExecuteProcess(cControlPrefix | 'tp_get_log_file_names', 'pExecutionId', pExecutionId,
	'pProcess', cTM1Process, 'pControl', pControl);
	If (vReturnValue <> ProcessExitNormal());
		ProcessError;
	EndIf;
Endif;

cPrologLog = gPrologLog;
cEpilogLog = gEpilogLog;
cDataLog = gDataLog;
cTM1Log = cPrologLog;

#*** Log Parameters

If (cGenerateLog @= 'Y');
	TextOutput(cTM1Log, 'Parameters:', TIMST(NOW, '\Y-\m-\d \h:\i:\s'), pExecutionId, pCube, pView, 'Y');
EndIf;

cDrillPrefix = 'DRILL.';
cDrillPrefixLength = LONG(cDrillPrefix);

If (pCube @<> '' & CubeExists(pCube) <> 0);
	If (cGenerateLog @= 'Y');
		If (LONG(pView) >= cDrillPrefixLength & SUBST(pView, 1, cDrillPrefixLength) @= cDrillPrefix);
			TextOutput(cTM1Log, 'View has correct prefix:', TIMST(NOW, '\Y-\m-\d \h:\i:\s'), pCube, pView);
		Else;
			TextOutput(cTM1Log, 'View has wrong prefix:', TIMST(NOW, '\Y-\m-\d \h:\i:\s'), SUBST(pView, 1, cDrillPrefixLength), STR(cDrillPrefixLength, 3, 0));
		EndIf;
		If (ViewExists(pCube, pView) <> 0);
			TextOutput(cTM1Log, 'View does exist:', TIMST(NOW, '\Y-\m-\d \h:\i:\s'), pCube, pView);
		Else;
			TextOutput(cTM1Log, 'View does not exist:', TIMST(NOW, '\Y-\m-\d \h:\i:\s'), pCube, pView);
		EndIf;
	EndIf;
	If (LONG(pView) >= cDrillPrefixLength & SUBST(pView, 1, cDrillPrefixLength) @= cDrillPrefix & ViewExists(pCube, pView) <> 0);
		If (cGenerateLog @= 'Y');
			TextOutput(cTM1Log, 'Executing ViewDestroy:', TIMST(NOW, '\Y-\m-\d \h:\i:\s'), pCube, pView);
		EndIf;
		ViewDestroy(pCube, pView);
		cSubsetName = pView;
		If (cGenerateLog @= 'Y');
			TextOutput(cTM1Log, 'Search Cube Dimensions for Subset:', TIMST(NOW, '\Y-\m-\d \h:\i:\s'), pCube, cSubsetName);
		EndIf;
		vDimIndex = 1;
		cDimName = TABDIM(pCube, vDimIndex);
		While ( cDimName @<> '' );
			If (SubsetExists(cDimName, cSubsetName) = 1);
				If (cGenerateLog @= 'Y');
					TextOutput(cTM1Log, 'Subset exists, delete the subset:', TIMST(NOW, '\Y-\m-\d \h:\i:\s'), pCube, cDimName, cSubsetName);
				EndIf;
				SubsetDestroy(cDimName, cSubsetName);
			Else;
				If (cGenerateLog @= 'Y');
					TextOutput(cTM1Log, 'Subset does not exist:', TIMST(NOW, '\Y-\m-\d \h:\i:\s'), pCube, cDimName, cSubsetName);
				EndIf;
			EndIf;
			vDimIndex = vDimIndex + 1;
			cDimName = TABDIM(pCube, vDimIndex);
		End;
	Else;
		If (cGenerateLog @= 'Y');
			TextOutput(cTM1Log, 'View does not exist:', TIMST(NOW, '\Y-\m-\d \h:\i:\s'), pCube, pView);
		EndIf;
	EndIf;
Else;
	If (cGenerateLog @= 'Y');
		TextOutput(cTM1Log, 'Cube does not exist:', TIMST(NOW, '\Y-\m-\d \h:\i:\s'), pCube);
	EndIf;
EndIf;

#endregion